#!/usr/bin/perl

sub on_user_command
{
	my($self,$cmd) = @_;

	if( $cmd eq "font:increase" ) {
		font_change_size( $self,  1);
	} elsif ($cmd eq "font:decrease") {
		font_change_size( $self,  -1);
	}
}

sub font_change_size
{
	my($term, $change) = @_;

	my ($fontstring) = $term->resource('font');

#	$term->scr_add_lines("\r\nCurrent is $fontstring\n");

	#-xos4-terminus-medium-r-normal-*-12-*-*-*-*-*-*-1

	my @fields = qw( foundry family weight slant setwidth style pixelSize pointSize Xresolution Yresolution spacing averageWidth registry encoding );

	my %font;
	$fontstring =~ s/^-//;  # Strip leading - before split
	@font{@fields} = split(/-/,$fontstring);

	if( $font{registry} eq '*' ) {
		$font{registry} ='iso8859';
	}

	# Blank out the size for the pattern
	my %pattern = %font;
	$pattern{pixelSize} = '*';
	$pattern{pointSize} = '*';
	$pattern{averageWidth} = '*';
	# make sure there are no empty fields
	foreach my $field (@fields) {
		$pattern{$field} = '*' unless defined($pattern{$field});
	}
	my $new_fontstring = '-' . join('-', @pattern{@fields});

	my @possible;
#	$term->scr_add_lines("\r\nPattern is $new_fontstring\n");
	open(FOO, "xlsfonts -fn '$new_fontstring' | sort -u |") or die $!;
	while(<FOO>) {
		chomp;
		s/^-//;  # Strip leading - before split
		my @fontdata = split(/-/,$_);

		# zero-size font is stupid.  Avoid.
		next if $fontdata[6] == 0;

		push @possible, [ $fontdata[6], "-$_" ];
#		$term->scr_add_lines("\r\npossibly $fontdata[6] $_\n");
	}
	close(FOO);

	if( ! @possible ) {
		die "No possible fonts!";
	}

	my $prev_font = undef;
	my $next_font = undef;
	foreach my $candidate ( @possible ) {
		if( $font{pixelSize} > $candidate->[0] ) {
			$prev_font = $candidate->[1];
		}
		if( $font{pixelSize} < $candidate->[0] ) {
			$next_font = $candidate->[1];
			last;
		}
	}

	my $newname;
	if( $change > 0 ) {
		$newname = $next_font;
	} elsif( $change < 0 ) {
		$newname = $prev_font;
	} else {
		die "Change cannot be zero!";
	}

#	$term->scr_add_lines("\rDEBUG: new font is $newname\n");

	# And change the font
	$term->cmd_parse( "\033]710;$newname\007") if $newname;
}
